fmod PRINCIPAL is
  sort Prin .
  op eve : -> Prin [ctor] .
endfm

fmod POLYNOMIAL is
  pr INT .
  sort Poly .
  subsort Int < Poly .

  op _p+_ : Poly Poly -> Poly [ctor assoc comm prec 33] .
  op _p*_ : Poly Poly -> Poly [ctor assoc comm prec 31] .
  op _md_ : Poly Nat -> Poly [ctor prec 32] .
  op _p-_ : Poly Poly -> Poly [prec 33] .
  op neg_ : Poly -> Poly [ctor] .

  vars P0 P1 P2 P3 : Poly .
  vars M N : Nat .
  vars K K2 : NzNat .
  vars A B : Int .
  eq P1 p+ 0 = P1 .
  eq P1 p* 0 = 0 .
  eq P1 p* 1 = P1 .
  eq P1 p* (P2 p+ P3) = (P1 p* P2) p+ (P1 p* P3) .

--- poly
  eq (K p* P1) md K = 0 .
  eq (P1 p+ (P2 md K)) md K = (P1 p+ P2) md K .

---subtraction
  eq P1 p- P2 = P1 p+ neg(P2) .
  eq P1 p+ neg(P1) = 0 . 
  eq neg(neg(P1)) = P1 .
  eq neg(P1 p+ P2) = neg(P1) p+ neg(P2) .
  eq neg(P1 md K) = neg(P1) md K .

--- bit shift
--- shift right
  op shiftR : Poly Int -> Poly .

--- borrow from maude
  op _^_ : Int Int -> Int [prec 29 gather (E e) special (
    id-hook NumberOpSymbol (^)
    op-hook succSymbol (s_ : Nat ~> NzNat)
    op-hook minusSymbol (-_ : NzNat ~> Int))] .
endfm

view Poly from TRIV to POLYNOMIAL is
  sort Elt to Poly .
endv

fmod VECTOR is
  pr POLYNOMIAL .
  sort Vector .
  subsort Poly < Vector .
  op tpV : Vector -> Vector .        ---- transpose to a row vector

--- inner product
--- actually, the 1st (or 2nd) should be transposed
--- we consider only dot of two vectors having the same size
  op _dot_ : Vector Vector -> Poly [prec 31] .
  op _v+_ : Vector Vector -> Vector [assoc comm prec 33] .
  op _mdv_ : Vector Nat -> Vector [prec 32] . --- vector mod p

  vars V1 V2 V3 : Vector .
  vars P1 P2 P3 : Poly .
  eq (V1 v+ V2) dot V3 = (V1 dot V3) p+ (V2 dot V3) .
  eq V3 dot (V1 v+ V2) = (V3 dot V1) p+ (V3 dot V2) .
  eq tpV(tpV(V1)) = V1 .
  eq tpV(V1 v+ V2) = tpV(V1) v+ tpV(V2) .

  var K : Nat .
  --- eq (V1 mdv K) mdv K = V1 mdv K .
  --- eq ((V1 mdv K) v+ V2) mdv K = (V1 v+ V2) mdv K .

--- bit shift
--- shift right
  op shiftRV : Vector Int -> Vector .
endfm

--- only consider square matrix
fmod MATRIX is
  pr VECTOR .
  sort Matrix .
  subsort Vector < Matrix .
  op tp : Matrix -> Matrix .        ---- transpose matrix
  op _m*_ : Matrix Vector -> Vector [prec 31] .

  vars M M2 : Matrix .
  vars V V2 : Vector .
  vars I J : Nat .
  vars P1 P2 P3 : Poly .

  eq tp(tp(M)) = M .
  --- eq tp(M m* M2) = tp(M2) m* tp(M) .

  op isSmall? : Matrix -> Bool .
  eq isSmall?(tp(M)) = isSmall?(M) .
  eq isSmall?(tpV(V)) = isSmall?(V) .
  ceq isSmall?(V v+ V2) = true 
    if (isSmall?(V) and isSmall?(V2)) .
  ceq isSmall?(V dot V2) = true 
    if (isSmall?(V) and isSmall?(V2)) .
  ceq isSmall?(P1 p- P2) = true 
    if (isSmall?(P1) and isSmall?(P2)) .
  ceq isSmall?(P1 p+ P2) = true 
    if (isSmall?(P1) and isSmall?(P2)) .
  ceq isSmall?(M m* M2) = true 
    if (isSmall?(M) and isSmall?(M2)) .

  --- eq tpV(M m* V) dot V2 = tpV(V) dot (tp(M) m* V2) .
endfm

fmod POLY-PAIR is
  pr POLYNOMIAL .
  sort PolyPair .
  --- subsort Poly < PolyPair .

  op pair : Poly Poly -> PolyPair [ctor] .
  op 1st : PolyPair -> Poly .
  op 2nd : PolyPair -> Poly .

  vars H0 H1 E0 E1 : Poly .
  eq 1st(pair(E0,E1)) = E0 .
  eq 2nd(pair(E0,E1)) = E1 .
endfm

fmod POLY-VECTOR-PAIR is
  pr VECTOR .
  sort PVPair .
  --- subsort Poly < PolyPair .

  op pvPair : Poly Vector -> PVPair [ctor] .
  op 1st : PVPair -> Poly .
  op 2nd : PVPair -> Vector .

  vars E0 E1 : Poly .
  vars V0 V1 : Vector .
  eq 1st(pvPair(E0,V0)) = E0 .
  eq 2nd(pvPair(E0,V0)) = V0 .
  eq (pvPair(E0,V0) == pvPair(E1,V1)) 
    = (E0 == E1 and V0 == V1) . 
endfm

fmod ENCRYPTION is
  pr POLY-PAIR + POLY-VECTOR-PAIR + MATRIX .

--- hash functions
  op F : PVPair -> Poly .
  op G : Poly Poly -> PolyPair .
  op H : Poly PVPair -> Poly .
endfm

fmod SAMPLING is
  pr MATRIX .

  op gen-A : Poly -> Matrix .
  op gen-s : Poly -> Vector .

  vars P : Poly .
  eq isSmall?(gen-s(P)) = true .
endfm

fmod MSG-STATE is
  sort MState .
  ops sent replied intercepted : -> MState .
endfm

fmod MESSAGE is
  pr PRINCIPAL + POLY-PAIR + POLY-VECTOR-PAIR + MATRIX + MSG-STATE .
  sort Msg .

--- seed_A and b
  op msg1 : Prin Prin Prin PVPair MState -> Msg [ctor] .

--- c_m and b'
  op msg2 : Prin Prin Prin PVPair MState -> Msg [ctor] .
endfm

fmod KEY is 
  pr POLYNOMIAL .
  pr PRINCIPAL .
  sort Key .
  op nil : -> Key [ctor] .
  op key : Poly Prin -> Key [ctor] .
endfm

fmod COLLECTION{X :: TRIV} is
  pr NAT .
  sort Collection{X} .
  subsort X$Elt < Collection{X} .
  op empty : -> Collection{X} [ctor] .
  op _;_ : Collection{X} Collection{X} -> Collection{X} [assoc comm id: empty ctor] .
  op _\in_ : X$Elt Collection{X} -> Bool .

  var E : X$Elt .
  var C : Collection{X} .
  eq E \in (E ; C) = true .
  eq E \in C = false [owise] .
endfm

fmod SLIST{X :: TRIV} is
  pr NAT .
  sort List{X} .
  subsort X$Elt < List{X} .
  op null : -> List{X} [ctor] .
  op _,_ : List{X} List{X} -> List{X} [assoc id: null ctor] .
endfm

view Msg from TRIV to MESSAGE is
  sort Elt to Msg .
endv
view Prin from TRIV to PRINCIPAL is
  sort Elt to Prin .
endv
view Key from TRIV to KEY is
  sort Elt to Key .
endv

fmod SOUP{D :: TRIV} is
  sort Soup{D} .
  subsort D$Elt < Soup{D} .
  op empty : -> Soup{D} [ctor] .
  op __ : Soup{D} Soup{D} -> Soup{D} [ctor assoc comm id: empty] .
endfm

fmod OCOMP is
  pr ENCRYPTION .
  pr MATRIX .
  pr COLLECTION{Poly} * (sort Collection{Poly} to ColPoly ) .
  pr SLIST{Poly} * (sort List{Poly} to ListPoly ) .
  pr COLLECTION{Msg} * (sort Collection{Msg} to ColMsg ) .
  pr COLLECTION{Prin} * (sort Collection{Prin} to ColPrin ) .
  pr COLLECTION{Key} * (sort Collection{Key} to ColKey ) .

  sort OComp .
  --- observable components
  op (nw:_) : ColMsg -> OComp [ctor] .  --- network
  op (keys[_]:_) : Prin ColKey -> OComp [ctor] .
  op (prins:_) : ColPrin -> OComp [ctor] .

--- alice side
  op (seed[_]:_) : Prin Poly -> OComp [ctor] .
  op (r[_]:_) : Prin Poly -> OComp [ctor] .
  
--- bob side
  op (m[_]:_) : Prin Poly -> OComp [ctor] .

--- random parts
  op (rd-seed:_) : ListPoly -> OComp [ctor] .
  op (rd-r:_) : ListPoly -> OComp [ctor] .
  op (rd-m:_) : ListPoly -> OComp [ctor] .

--- eve 
  op (seeds:_) : ColPoly -> OComp [ctor] .
  op (rs:_) : ColPoly -> OComp [ctor] .
  op (ms:_) : ColPoly -> OComp [ctor] .
  op (glean-keys:_) : ColKey -> OComp [ctor] . --- key obtained by eve
endfm

view OComp from TRIV to OCOMP is
  sort Elt to OComp .
endv

fmod CONFIG is
  pr SOUP{OComp} .
  sort Config .
  op {_} : Soup{OComp} -> Config [ctor] .
  op init : -> Config .

--- constants, known by everybody
  ops esp esq esT : -> NzNat .
  ops h1 h2 : -> Poly .
  ops h : -> Vector .
  ops p q t : -> NzNat .
  --- eq p = 2 ^ esp .
  --- eq q = 2 ^ esq .
  --- eq t = 2 ^ esT .

  ops seed1 seed2 r1 r2 m1 m2 : -> Poly .
  ops alice bob : -> Prin .

  eq init = {(nw: empty) (prins: (alice ; bob ; eve))
    (rd-seed: (seed1, seed2)) (rd-r: (r1 , r2)) (rd-m: (m1 , m2)) 
    (keys[alice]: empty) (keys[bob]: empty) (glean-keys: empty)
    (seed[alice]: 0) (seed[bob]: 0) (r[alice]: 0) (r[bob]: 0)
    (m[alice]: 0) (m[bob]: 0)
    (seeds: empty) (rs: empty) (ms: empty)} .

--- some approximation
  vars P1 P2 P3 M CM : Poly .
  vars S S' B B' : Vector .
  vars MA : Matrix .
  vars K Q K2 PT 2PT 2P1 : Int .

--- approximation
  ceq tpV(shiftRV( (MA m* S' v+ h) mdv q, esq - esp )) dot S
    p+ neg(tpV(shiftRV( (tp(MA) m* S v+ h) mdv q, esq - esp )) dot S') = 0
  if isSmall?(S) and isSmall?(S') . 

  ceq 2PT p* shiftR(CM, PT) = CM 
  if PT := esp - esT /\
     2PT := 2 ^ PT .

  ceq shiftR( (h2 p+ neg(h1) p+ 2P1 p* M) md p, esp - 1) = M
  if 2P1 := 2 ^ (esp - 1) .
endfm

mod SABER is 
  pr CONFIG + SAMPLING .
  vars A B C : Prin .
  vars PC1 PC2 PC3 PC4 : ColPoly .
  vars KS : ColKey .
  vars PL PL2 PL3 : ListPoly .
  vars K1 K2 K3 : Poly .
  vars KE1 KE2 KE3 : Key .
  vars MS MS2 MS3 : ColMsg .
  vars N : Nat .
  var OCs : Soup{OComp} . 
  vars PS : ColPrin .
  vars M M2 SD M' R V V' P1 P2 CM SD-A CM-B : Poly .
  vars VB VB' PK T U UB TA S S' VB-A VB'-B : Vector .
  vars MG1 MG2 MG3 MG4 : Msg .
  vars MsgStat MsgStat2 : MState .
  vars Kr Kr' RoSig : PolyPair .
  vars MA : Matrix .
  vars CB CB' : PVPair .

--- PKE.enc: seed, b, m, r
  op enc : Poly Vector Poly Poly -> PVPair .
  ceq enc(SD, VB, M, R) = 
    pvPair(CM, VB')
  if MA := gen-A(SD) /\
    S' := gen-s(R) /\
    VB' := shiftRV((MA m* S' v+ h) mdv q, esq - esp) /\
    V' := tpV(VB) dot S' /\
    CM := shiftR( ((V' p+ h1) p- (2 ^ (esp - 1)) p* M) md p, esp - esT) .

--- keygen
  crl [keygen] : {(rd-seed: (SD, PL)) (rd-r: (R, PL2)) 
    (seed[A]: P1) (r[A]: P2) 
    (prins: (A ; B ; PS)) (nw: MS) OCs}
  => {(rd-seed: PL) (rd-r: PL2) (seed[A]: SD) (r[A]: R) 
    (prins: (A ; B ; PS)) 
    (nw: (MS ; msg1(A,A,B, pvPair(SD, VB), sent))) OCs} 
  if MA := gen-A(SD) /\
    S := gen-s(R) /\
    VB := shiftRV((tp(MA) m* S v+ h) mdv q, esq - esp) .

--- encap
  crl [encaps] : {(rd-m: (M, PL)) (m[B]: P1) (keys[B]: KS)
    (nw: (msg1(C,A,B, pvPair(SD,VB), sent) ; MS)) OCs}
  => {(rd-m: PL) (m[B]: M) 
    (keys[B]: (KS ; key( H(1st(Kr), CB) , A)))
    (nw: (msg1(C,A,B, pvPair(SD, VB), replied) ; 
      msg2(B,B,A, CB, sent) ;
      MS)) OCs} 
  if Kr := G(F(pvPair(SD,VB)), M) /\
    CB := enc(SD,VB,M,2nd(Kr)) .

--- decap
  crl [decaps] : {(seed[A]: SD) (r[A]: R)  (keys[A]: KS)
    (nw: (msg1(A,A,B, pvPair(SD,VB), MsgStat) ; 
      msg2(C,B,A, pvPair(CM,VB'), sent) ; MS)) OCs}
  => {(seed[A]: SD) (r[A]: R) 
    (keys[A]: (KS ; key( H(1st(Kr'),CB') , B)))
    (nw: (msg1(A,A,B, pvPair(SD,VB), MsgStat) ;
      msg2(C,B,A, pvPair(CM,VB'), replied) ; MS)) OCs} 
  if MA := gen-A(SD) /\
    S := gen-s(R) /\
    VB == shiftRV((tp(MA) m* S v+ h) mdv q, esq - esp) /\
    V := tpV(VB') dot S /\
    M' := shiftR(((V p+ h2) p- 2 ^ (esp - esT) p* CM) md p, esp - 1) /\
    Kr' := G(F(pvPair(SD,VB)), M') /\
    CB' := enc(SD,VB,M',2nd(Kr')) /\
    CB' == pvPair(CM,VB') .

  rl [stutter] : {(rd-seed: null) OCs} => {(rd-seed: null) OCs} .

***(
  * eve intercepts a message msg1 sent from alice to bob and
  * sends a fake message to bob
)
  rl [build-sds] : {(rd-seed: (SD, PL)) (seeds: PC1) OCs}
  => {(rd-seed: PL) (seeds: (SD ; PC1)) OCs} .

  rl [build-rs] : {(rd-r: (R, PL2)) (rs: PC2) OCs}
  => {(rd-r: PL2) (rs: (R ; PC2)) OCs} .

  rl [build-ms] : {(rd-m: (M, PL)) (ms: PC1) OCs}
  => {(rd-m: PL) (ms: (M ; PC1)) OCs} .

  crl [keygen-eve] : {(seeds: (SD ; PC1)) (rs: (R ; PC2))
    (nw: (msg1(A,A,B, pvPair(SD-A,VB-A), sent) ; MS)) OCs}
  => {(seeds: (SD ; PC1)) (rs: (R ; PC2))
    (nw: (msg1(A,A,B, pvPair(SD-A,VB-A), intercepted) ;
      msg1(eve,A,B, pvPair(SD, VB), sent) ; MS)) OCs}
  if MA := gen-A(SD) /\
    S := gen-s(R) /\
    VB := shiftRV((tp(MA) m* S v+ h) mdv q, esq - esp) .

  crl [encaps-eve] : {(ms: (M ; PC3))
    (nw: (msg1(A,A,B, pvPair(SD-A,VB-A), intercepted) ; MS)) 
    (glean-keys: KS) OCs}
  => {(ms: (M ; PC3))
    (nw: (msg1(A,A,B, pvPair(SD-A,VB-A), intercepted) ;
      msg2(eve,B,A, CB, sent) ; MS)) 
    (glean-keys: (KS ; key(H(1st(Kr), CB), A)) ) OCs} 
  if Kr := G(F(pvPair(SD-A,VB-A)), M) /\
    CB := enc(SD-A,VB-A,M,2nd(Kr)) /\
    msg2(eve,B,A, CB, sent) \in MS = false /\
    msg2(eve,B,A, CB, replied) \in MS = false .

  crl [decaps-eve] : {(seeds: (SD ; PC1)) (rs: (R ; PC2)) 
    (ms: (M ; PC3))
    (nw: (msg1(eve,A,B, pvPair(SD,VB), replied) ;
      msg2(B,B,A, pvPair(CM-B,VB'-B), sent) ; MS)) 
    (glean-keys: KS) OCs}
  => {(seeds: (SD ; PC1)) (rs: (R ; PC2)) (ms: (M ; PC3))
    (nw: (msg1(eve,A,B, pvPair(SD,VB), replied) ;
      msg2(B,B,A, pvPair(CM-B,VB'-B), intercepted) ; MS)) 
    (glean-keys: (KS ; key(H(1st(Kr'),CB'), B)) ) OCs} 
  if MA := gen-A(SD) /\
    S := gen-s(R) /\
    VB == shiftRV((tp(MA) m* S v+ h) mdv q, esq - esp) /\
    V := tpV(VB'-B) dot S /\
    M' := shiftR(((V p+ h2) p- 2 ^ (esp - esT) p* CM-B) md p, esp - 1) /\
    Kr' := G(F(pvPair(SD,VB)), M') /\
    CB' := enc(SD,VB,M',2nd(Kr')) /\
    CB' == pvPair(CM-B,VB'-B) .
endm

search [1] in SABER : 
  init =>* {(keys[alice]: key(K1,bob)) (keys[bob]: key(K2,alice)) 
    (glean-keys: (key(K1,alice) ; key(K2,bob) ; KS)) OCs} .
eof

search in SABER : init =>* {OCs} . 
eof